plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'signing'
}

android {
    namespace 'otang.pulse.lib'
    compileSdk 34
    buildToolsVersion '33.0.2'

    defaultConfig {
        minSdk 26
        targetSdk 34
        vectorDrawables {
            useSupportLibrary true
        }
        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }

}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'com.google.android.material:material:1.9.0'
}

ext {
    PUBLISH_GROUP_ID = 'io.github.otang45'
    PUBLISH_VERSION = '2.3'
    PUBLISH_ARTIFACT_ID = 'Pulse'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = "Audio Visualizer"
                    url = 'https://github.com/Otang45/Pulse'
                    licenses {
                        license {
                            name = 'MIT license'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'dikiotang'
                            name = 'Otang45'
                            email = 'dikiotang@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/Otang45/Pulse.git'
                        developerConnection = 'scm:git:ssh://github.com/Otang45/Pulse.git'
                        url = 'https://github.com/Otang45/Pulse'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"]
    )
    sign publishing.publications
}
