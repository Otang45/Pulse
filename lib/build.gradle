
plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'signing'
}

ext {
    PUBLISH_GROUP_ID = 'io.github.otang45'
    PUBLISH_VERSION = '1.0'
    PUBLISH_ARTIFACT_ID = 'Pulse'
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll setup later
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION
                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }
                // artifact androidSourcesJar
                // artifacts javaDocJar
                // Mostly self-explanatory metadata
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = "Audio Visualizer"
                    url = 'https://github.com/Otang45/Pulse'
                    licenses {
                        license {
                            name = 'MIT license'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'dikiotang'
                            name = 'Otang45'
                            email = 'dikiotang@gmail.com'
                        }
                    }
                    // Version control info
                    scm {
                        connection = 'scm:git:github.com/Otang45/Pulse.git'
                        developerConnection = 'scm:git:ssh://github.com/Otang45/Pulse.git'
                        url = 'https://github.com/Otang45/Pulse'
                    }
                }
                generateMetadataFileForReleasePublication.dependsOn androidSourcesJar
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"]
    )
    sign publishing.publications
}

android {
	namespace 'otang.pulse.lib'
    compileSdk 34
    buildToolsVersion '33.0.2'

    defaultConfig {
        minSdk 26
        targetSdk 34

        vectorDrawables {
            useSupportLibrary true
        }
        multiDexEnabled true
    }
    
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
    }

}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.core:core-ktx:1.10.1'
	implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'com.google.android.material:material:1.9.0'
}
